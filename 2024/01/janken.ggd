%% Janken, a.k.a. "Rock-Paper-Scissors" or Roshambo.
%%
%% Perhaps the simplest game I can think of for describing with
%% any GDL variant.  Despite its simplicity, here are demonstrated
%% some improvements that GGDL provides when defining relations.

%% Roles may be defined together, and any structured properties
%% on a role are given to all of them independently.
role left, right { show: Hand }

%% The traditional ordering of available hand gestures:
%% Paper covers rock, scissors cuts paper, rock breaks scissors.
data Hand := ROCK << PAPER << SCISSORS << ROCK

%% A player chooses one of the hand gestures as their move.
?player -> play(?hand)
  :- ?player in roles and ?hand in Hand
  ==> ?player.show = ?hand

%% The game is over when players choose different hands.
terminal :- left.show =/= right.show

%% Winner-takes-all outcome, preference order is defined by Hand.
left $= 100, right $= 0 :- left.show >> right.show
left $= 0, right $= 100 :- left.show << right.show
